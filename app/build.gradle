apply plugin: 'com.android.application'
apply plugin: 'realm-android'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.mastercard.labs.mpqrmerchant"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 11
        versionName "1.0.11"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        demo {
            applicationId "com.mastercard.labs.mpqrmerchant.demo"
        }
        ecobank {
            applicationId "com.mastercard.labs.mpqrmerchant.ecobank"
        }
        india {
            applicationId "com.mastercard.labs.mpqrmerchant.ecobank"
        }
        thailand {
            applicationId "com.mastercard.labs.mpqrmerchant.thailand"
        }
        sbi {
            applicationId "com.mastercard.labs.mpqrmerchant.sbi"
        }
    }

    sourceSets {
        main.java.srcDirs = ['src/main/java']
        main.res.srcDirs = ['src/main/res']
        main.assets.srcDirs = ['src/main/assets']

        ecobank {
            res.srcDirs = ['src/brands/ecobank/res']
            assets.srcDirs = ['src/brands/ecobank/assets']
        }
        india {
            res.srcDirs = ['src/brands/ecobank/res']
            assets.srcDirs = ['src/brands/ecobank/assets']
        }
        thailand {
            res.srcDirs = ['src/brands/thailand/res']
            assets.srcDirs = ['src/brands/thailand/assets']
        }
        sbi {
            res.srcDirs = ['src/brands/sbi/res']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name != "release")
            return

        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name.replace("-release.apk", "-merchant-release-${variant.versionName}.apk"))
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    //required dependencies
    compile files('libs/pushpayment-core-sdk-1.0.0.jar')
    compile 'com.journeyapps:zxing-android-embedded:3.4.0'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.0.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.google.android.gms:play-services-base:11.0.4'
    compile 'com.google.firebase:firebase-messaging:11.0.4'
    compile 'com.jakewharton:butterknife:8.5.0'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'com.android.support:design:25.3.1'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:retrofit-mock:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    testCompile 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.0'
}
apply plugin: 'com.google.gms.google-services'